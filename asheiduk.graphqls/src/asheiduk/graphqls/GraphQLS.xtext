/*
 * Modelled according to the specifiaction at
 *
 * https://graphql.github.io/graphql-spec/June2018/#sec-Type-System
 */
grammar asheiduk.graphqls.GraphQLS hidden (WS, SL_COMMENT, COMMA)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

// TODO: othe URI (?)
generate graphQLS "http://www.graphqls.asheiduk/GraphQLS"

// TODO: TypeSystemExtension?
// TODO: ExecutableDefinition?
Document:
	definitions += Definition*
;

Definition:
	TypeSystemDefinition
;

TypeSystemDefinition:
	SchemaDefinition
	| TypeDefinition
	| DirectiveDefinition
;

// ------------------------------------------------------------------------
// -- SchemaDefinition
// ------------------------------------------------------------------------

SchemaDefinition:
	'schema' Directives '{'
		rootOperations += RootOperationTypeDefinition*
	'}'
;

RootOperationTypeDefinition:
	operationType=OperationType ':' type=NamedType
;

enum OperationType:
	query | mutation | subscription
;

// ------------------------------------------------------------------------
// -- TypeDefinition
// ------------------------------------------------------------------------

TypeDefinition:
	ScalarTypeDefinition
	| ObjectTypeDefinition
	| InterfaceTypeDefinition
	| UnionTypeDefinition
	| EnumTypeDefinition
	| InputObjectTypeDefinition
;

// ------------------------------------------------------------------------

ScalarTypeDefinition:
	Description 'scalar' name=ID Directives
	;

// ------------------------------------------------------------------------

ObjectTypeDefinition:
	Description 'type' name=ID
	( 'implements' '&'? implements += NamedType ( '&' implements += NamedType)* )?
	Directives
	('{'
		fields += FieldDefinition*
	'}')?
;

FieldDefinition:
	Description	name=ID Arguments? ':' TypeSpec Directives
;

// ------------------------------------------------------------------------

InterfaceTypeDefinition:
	Description 'interface' name=ID Directives ('{'
		fields += FieldDefinition*
	'}')? 
;

// ------------------------------------------------------------------------

UnionTypeDefinition:
	Description 'union' name=ID Directives '='
		'|'? members += NamedType ('|' members += NamedType)*
;

// ------------------------------------------------------------------------

EnumTypeDefinition:
	Description 'enum' name=ID Directives ('{'
		values += EnumValueDefinition*
	'}')?
;

EnumValueDefinition:
	Description name=ID Directives
;

// ------------------------------------------------------------------------

InputObjectTypeDefinition:
	Description 'input' name=ID Directives ('{'
		fields += InputValueDefinition*
	'}')?
;

InputValueDefinition:
	Description name=ID ':' TypeSpec ('=' defaultValue=Value)? Directives
;

// ------------------------------------------------------------------------

/* Strictly correct but produces a grammar error:
 * 
 * 	rule ruleType has non-LL(*) decision due to recursive rule invocations reachable from alts 2,3.  Resolve by left-factoring or using syntactic predicates or using backtrack=true option.
 * 
 * The recursion happens due to "NonNullType". 
 * 
Type:
	NamedType | ListType | NonNullType
;

NamedType:
	type=[TypeDefinition]
;

ListType:
	'[' itemType=Type ']'
;

NonNullType:
	type=(NamedType | ListType) '!' 
;
 */

NamedType:
	predefinedType=PredefinedScalar
	| type=[TypeDefinition]	// TODO: Scoping?
;

Type:
	NamedTypeWithNotNull | ListTypeWithNotNull
;

// TODO: perhaps these two special rules can be eliminated with left-factoring 
NamedTypeWithNotNull:
	(
		predefinedType=PredefinedScalar
		| type=[TypeDefinition]	// TODO: Scoping?
	)
	nonNull ?= '!'?	
;

ListTypeWithNotNull:
	'[' itemType=Type ']' nonNull ?= '!'?
;

// ------------------------------------------------------------------------
// -- DirectiveDefinition
// ------------------------------------------------------------------------

DirectiveDefinition:
	Description 'directive' '@' name=ID Arguments?
	'on''|'?  locations += DirectiveLocation ( '|' locations += DirectiveLocation)*
;

enum DirectiveLocation:
	// ExecutableDirectiveLocation
	QUERY | MUTATION | SUBSCRIPTION | FIELD | FRAGMENT_DEFINITION | FRAGMENT_SPREAD | INLINE_FRAGMENT
	
	// TypeSystemDirectiveLocation
	| SCHEMA | SCALAR | OBJECT |FIELD_DEFINITION | ARGUMENT_DEFINITION
	| INTERFACE | UNION | ENUM | ENUM_VALUE | INPUT_OBJECT
	| INPUT_FIELD_DEFINITION
;

// ------------------------------------------------------------------------

Directive:
	// TODO: replace "name" with crossreference to prefined or user defined DirectiveDefinition
	'@' name=ID ( '(' arguments += Argument* ')' )?
;

Argument:
	name=ID ':' value=Value
;

// ------------------------------------------------------------------------

ArgumentDefinition:
	Description name=ID ':' TypeSpec ('=' defaultValue=Value)? Directives
;

Value:
//	Variable	// TODO
	{IntValue} value=INT
	| {FloatValue} value=FLOAT
	| {StringValue} value=STRING
	| {BooleanValue} value=('true' | 'false') 
	| {NullValue} 'null'
//	| EnumValue	// TODO
	| {ListValue} '[' items += Value* ']'
	| {ObjectValue} '{' fields += ObjectField* '}'
;

ObjectField:
	name=ID ':' value=Value
;

// ------------------------------------------------------------------

enum PredefinedScalar:
	Int | Float | String | Boolean | ID
;

// ------------------------------------------------------------------
// -- Grammar-Fragments
// ------------------------------------------------------------------

fragment Description*: 
	description=STRING?
;

fragment Directives*:
	directives += Directive*
;

fragment TypeSpec*:
	type=Type
;

fragment Arguments*:
	'(' arguments += ArgumentDefinition* ')'	
;

// ------------------------------------------------------------------
// -- Terminals
// ------------------------------------------------------------------

terminal INT returns ecore::EInt:
	INTEGER_PART;

terminal FLOAT returns ecore::EDouble:	// Really! See ยง3.5.2 - it is a double but called "Float"!
	INTEGER_PART FRACTIONAL_PART
	| INTEGER_PART EXPONENT_PART
	| INTEGER_PART FRACTIONAL_PART EXPONENT_PART;

terminal fragment INTEGER_PART:
	'-'? '0' | '-'? '1'..'9' '0'..'9'*;

terminal fragment FRACTIONAL_PART:
	'.' '0'..'9'*;	// TODO: * or +?

terminal fragment EXPONENT_PART:
	('e' | 'E') ('+' | '-')? '0'..'9'*;	// TODO: * or +?

// TODO: align with spec
terminal STRING:
	'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"'
	| '"""' -> '"""';

terminal ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

// -- hidden tokens

terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;

terminal COMMA: ',';

terminal WS: (' '|'\t'|'\r'|'\n')+;

// terminal ANY_OTHER: .;
