grammar asheiduk.graphqls.GraphQLS with org.eclipse.xtext.common.Terminals

generate graphQLS "http://www.graphqls.asheiduk/GraphQLS"

Schema:
	definitions += Definition*
	;

Definition:
	Type | Enum 
;

Type:
	('type' | input ?= 'input') name=ID '{'
		fields += Field*
	'}'
;

Field:
	name=ID 
	( '(' arguments += Argument (',' arguments += Argument)* ')' )?
	':' type=TypeSpec
;

Argument:
	name=XID ':' type=TypeSpec
;

TypeSpec:
	typeRef=TypeRef required ?= '!'?
	| isArray ?= '[' typeRef=TypeRef itemRequired ?= '!'? ']' required ?= '!'?
;

TypeRef:
	predefinedType=PredefinedType
	| type=[Definition|XID]
;

Enum:
	'enum' name=ID '{'
		literals += EnumLiteral*
	'}'
;

EnumLiteral:
	name=ID
;

enum PredefinedType:
	ID | String | Int | Float | Boolean
;

XID:
	ID
	| 'input'
	| 'type'
	| 'enum'
;

@Override 
terminal SL_COMMENT : '#' !('\n'|'\r')* ('\r'? '\n')?;

@Override 
terminal ML_COMMENT : '"""' -> '"""';
